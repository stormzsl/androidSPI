//↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓Jcenter第一部分，复制就可以，有注释的地方替换为你的内容↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group = projectGroupId

/**
 * 读取bintray属性
 */
def bintrayUser = properties.getProperty("bintray.user")  //bintray用户名
def bintrayApikey = properties.getProperty("bintray.apikey") //bintray Api Key
def bintrayOrganizationId = properties.getProperty("bintray.organizationId") //组织Id
def developerId = properties.getProperty("developer.id") //开发者Id
def developerName = properties.getProperty("developer.name") //开发者名称
def developerEmail = properties.getProperty("developer.email") //开发者email地址
//def projectVersionName = android.defaultConfig.versionName //项目版本号,在外面定义了



//↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓Jcenter第二部分，复制就可以，有注释的地方替换为你的内容↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓↓
bintray {
    user =bintrayUser
    key = bintrayApikey
    pkg {
        repo = repositoryName//自己创建的仓库名字
        name = projectName//上传到JCenter的名字,最终引用的名字 compile 'com.fasten:requestPermission:1.0.0
        websiteUrl = projectSiteUrl
        vcsUrl = projectGitUrl
        licenses = ['MIT']//不能随便写，只能是仓库创建时选择的license type
        userOrg = bintrayOrganizationId //自己创建的organization名称，一定要全是小写字母，即使当时创建的名称含有大写，这里全部转换为小写，否则404
        publish = true // 是否是公开项目，公开别人可以引用
        version {
            name = projectVersionName
            desc = projectDesc//描述，自己定义
            released  = new Date()
            vcsTag = projectVersionName
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
    configurations = ['archives']
}


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name projectName //项目名
                groupId projectGroupId //项目Id(一般为唯一包名)
                artifactId projectArtifactId //一般与项目名相同
                version projectVersionName //项目版本号
                packaging projectPackaging //打包类型
                url projectSiteUrl  //项目地址

                description projectDesc
                // Set your license
                licenses {
                    license {
                        name 'MIT' //和之前自己定义的协议一致
                        url 'https://raw.githubusercontent.com/minggo620/Pluto-Android/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id developerId        //填写bintray或者github的用户名
                        name developerName         //姓名
                        email developerEmail//邮箱
                    }
                }
                scm {
                    connection projectGitUrl
                    developerConnection projectGitUrl
                    url projectSiteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options{
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        version projectVersionName
        title javadocName
    }
}
//↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑↑
